global gLevel

on preOpenCard
   
   // Hide stars
   set the visible of btn "star1" to false
   set the visible of btn "star2" to false
   set the visible of btn "star3" to false
   set the visible of btn "star1_shadow" to false
   set the visible of btn "star2_shadow" to false
   set the visible of btn "star3_shadow" to false
   set the visible of btn "star1_outline" to false
   set the visible of btn "star2_outline" to false
   set the visible of btn "star3_outline" to false
   
   put the cWinMsg of this card into fld "win_message" // Display win message
   put the cNewHighScoreMsg of this card into fld "high_score_message" // Display any high-score message
     
   put the cCurrentLevel of this stack + 1 into tNextLevel // Get next level number
   
   if "level" & tNextLevel is among the lines of the customPropertySets of the current stack then // If the next level exists
      set the visible of btn "next_level" to true // Show "next level" button
   else
      set the visible of btn "next_level" to false // Hide "next level" button
   end if
   
   // Position and hide navigation
   put item 1 of the loc of this card into tCardLocX
   set the loc of grp "win_nav" to tCardLocX, 888
   set the visible of grp "win_nav" to false
   
end preOpenCard

on openCard
   
   put the cStarRating of this card into tStarRating
   
   if tStarRating is "3" then
      
      send "animateStar star1" to me in 300 millisecs
      send "animateStar star2" to me in 600 millisecs
      send "animateStar star3" to me in 900 millisecs
      send "showNav" to me in 1400 millisecs
      
   else if tStarRating is "2" then
      
      send "animateStar star1" to me in 300 millisecs
      send "animateStar star2" to me in 600 millisecs
      send "showNav" to me in 1100 millisecs
      
   else if tStarRating is "1" then
      
      send "animateStar star1" to me in 300 millisecs
      send "showNav" to me in 800 millisecs
      
   end if
   
end openCard

## Animates stars used in level rating
on animateStar pStarBtn
   
   put 300 into tAnimateSpeed
   put pStarBtn & "_outline" into tStarOutline
   put pStarBtn & "_shadow" into tStarShadow
   
   aeFadeIn the long id of btn tStarOutline, tAnimateSpeed // Fade in outlined star   
   send "transition tStarOutline, tStarShadow, tAnimateSpeed" to me in 300 millisecs // Fade out outlined star, fade in filled star with shadow   
   send "transition tStarShadow, pStarBtn, tAnimateSpeed" to me in 600 millisecs // Fade out filled star with shadow, fade in final star button

end animateStar

## Fade button 1 out, fade button 2 in
on transition pBtn1, pBtn2, pSpeed
   
   aeFadeOut the long id of btn pBtn1, pSpeed
   aeFadeIn the long id of btn pBtn2, pSpeed
   
end transition

## Fade in navigation
on showNav
   aeFadeIn the long id of grp "win_nav", 500
end showNav
